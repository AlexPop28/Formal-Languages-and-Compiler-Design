#+BEGIN_SRC 
program = {statement}
statement = ((variable_declaration | function_call | assignment) ";") | if | while

type = "int" | "str" | "double"

variable_declaration =
  | type " " identifier
  | type " " identifier "[" int_constant "]"

function_call =
  | get_call
  | set_call
  | read_call
  | print_call

get_call = "get" "(" identifier "," int_expression ")"
set_call = "set" "(" identifier "," int_expression "," expression ")"
read_call = ("read_int" | "read_str" | "read_double") "(" ")"
print_call = ("print_int" | "print_str" | "print_double") "(" expression ")"

assignment = identifier "=" expression

if = "if" "(" bool_expression ")" "{" program "}" ["else" "{" program "}"]
while = "while" "(" bool_expression ")" "{" program "}"

constant = int_constant | str_constant | double_constant

bool_operator = "==" | "!=" | "<" | ">" | "<=" | ">="
bool_expression = (constant | identifier) bool_operator (constant | identifier)

expression = int_expression | str_expression | double_expression
  int_expression = int_term | int_term ("+" | "-") int_expression
    int_term = int_factor | int_factor ("*" | "/" | "%") int_term 
      int_factor = int_constant | identifier | "(" int_expression ")"
  double_expression = double_term | double_term ("+" | "-") double_expression
    double_term = double_factor | double_factor ("*" | "/") double_term 
      double_factor = double_constant | identifier | "(" double_expression ")"
  str_expression = str_term | str_term "+" str_expression
    str_term = str_constant | identifier
#+END_SRC
